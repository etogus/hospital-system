buildscript {
	ext {
		protobufVersion = '3.23.4'
		protobufPluginVersion = '0.8.18'
		grpcVersion = '1.58.0'
		grpcSpringBootStarterVersion = '2.15.0.RELEASE'
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.1'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.google.protobuf' version "${protobufPluginVersion}"
}

group = 'com.guseinma'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
	implementation "net.devh:grpc-server-spring-boot-starter:${grpcSpringBootStarterVersion}"
	runtimeOnly 'com.h2database:h2'
	compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5' // Java 9+ compatibility - Do NOT update to 2.0.0
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protobufVersion}"
	}
	generatedFilesBaseDir = "$projectDir/src/generated"
	clean {
		delete generatedFilesBaseDir
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'src/generated/main/grpc'
			srcDirs 'src/generated/main/java'
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

task runGrpcClient(type: JavaExec) {
	classpath = sourceSets.test.runtimeClasspath
	mainClass = 'com.guseinma.hospital.client.GrpcClient'
}

tasks.withType(JavaCompile) {
	it.dependsOn 'generateProto'
}
